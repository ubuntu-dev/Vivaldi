// Vivaldi version of a Brainfuck interpreter
// 
// Based of Ruby implementation: https://github.com/buren/bf-interpreter/blob/master/bf.rb
class Brainfuck

  let init(code) = do
    @code     = code
    @tape     = []
    @code_pos = 0
    @tape_pos = 0
  end

  let run(skip) = do
    while (@tape_pos >= 0 && @code_pos < @code.size()): do
      if (@tape_pos >= @tape.size()): @tape.append(0)

      cond
        @code[@code_pos] == \[: self.do_inc(),
        @code[@code_pos] == \]: return @tape[@tape_pos] != 0,
        !skip:                  self.do_code()

      @code_pos = @code_pos + 1
    end
  end

  let do_inc() = do
    @code_pos = @code_pos + 1
    let old_pos   = @code_pos
    while self.run(@tape[@tape_pos] == 0): do
      @code_pos = old_pos
    end
  end

  let do_code() = do
    let code_char = @code[@code_pos]
    cond
      code_char == \+: @tape[@tape_pos] = @tape[@tape_pos] + 1,
      code_char == \-: @tape[@tape_pos] = @tape[@tape_pos] - 1,
      code_char == \>: @tape_pos = @tape_pos + 1,
      code_char == \<: @tape_pos = @tape_pos - 1,
      code_char == \.: self.print_char(@tape[@tape_pos]),
      code_char == \,: @tape[@tape_pos] = next_char_ord
  end

  let print_char(char) = do
    print(char.chr())
  end

  let next_char_ord() = do
    gets().ord()
  end
end

let hello_world = "++++++++[>++++[>++>+++>+++>+<<<<-]>+>+>->>+[<]<-]>>.>---.+++++++..+++.>>.<-.<.+++.------.--------.>>+.>++."
new Brainfuck(hello_world).run(false)
